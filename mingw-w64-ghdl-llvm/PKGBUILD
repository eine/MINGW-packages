_realname=ghdl-llvm
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=dev
pkgrel=1
pkgdesc="GHDL: the open-source analyzer, compiler, simulator and (experimental) synthesizer for VHDL (LLVM backend) (mingw-w64)"
arch=('x86_64')
license=('GPL2+')
url="http://github.com/ghdl"
provides=('ghdl')
conflicts=('ghdl-mcode')
depends=('zlib-devel' 'mingw-w64-x86_64-clang')
makedepends=("${MINGW_PACKAGE_PREFIX}-clang" "${MINGW_PACKAGE_PREFIX}-gcc-ada")
source=("ghdl::git://github.com/ghdl/ghdl.git#commit=e2d751d6")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/ghdl"
  # Date of the last git commit
  _verdate=`git log -1 --date=short --pretty=format:%cd`
  if `git describe --exact-match > /dev/null 2>&1` ; then
    echo "`git describe --tags`.$_verdate" | sed 's/-//g'
  else
    git describe --tags --abbrev=10 | sed "s/\([!-]*\)-\(.*\)/\1.$_verdate.\2/g" | sed 's/-//g'
  fi;
}

build() {
  mkdir "${srcdir}/builddir"
  cd "${srcdir}/builddir"
  ../ghdl/configure --prefix=${MINGW_PREFIX} --with-llvm-config="llvm-config --link-static" LDFLAGS="-static" --enable-libghdl --enable-synth
  make GNATMAKE="gnatmake -j$(nproc)"
}

# FIXME: Cannot run tests because expected failures make 'check()' exit with error: A failure occurred in check()

package() {
  cd "${srcdir}/builddir"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib"
  make DESTDIR="${pkgdir}" install
}
